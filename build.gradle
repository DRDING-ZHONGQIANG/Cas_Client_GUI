import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

import static org.gradle.api.tasks.wrapper.Wrapper.DistributionType.*

buildscript {
    ext {
        springBootVersion = '1.5.4.RELEASE'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

plugins {
    id "com.zoltu.git-versioning" version "3.0.3"
    id 'pl.allegro.tech.build.axion-release' version '1.5.0'
    id 'nebula.optional-base' version '3.1.0'
}

apply plugin: 'base'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'

ZoltuGitVersioning {
    customDescribeProcessor { describeResults ->
        def matcher = (describeResults =~ /[0-9A-Za-z\.\-]*([0-9]+?)\.([0-9]+?)\.([0-9]+?)(?:\-([0-9A-Za-z\.\-]+))?\-([0-9]+?)\-g([a-zA-Z0-9]+)/)
        matcher.matches()
        [
                major      : matcher.group(1),
                minor      : matcher.group(2),
                patch      : matcher.group(3),
                tags       : matcher.group(4),
                commitCount: matcher.group(5),
                sha        : matcher.group(6),
        ]
    }
}

ext {
    buildNumber = System.getenv().BUILD_NUMBER
    versionInfo = ZoltuGitVersioning.versionInfo
    currentDate = DateTimeFormatter.ISO_LOCAL_DATE.format(LocalDateTime.now())
    versionProperties = [
            version    : version,
            buildNumber: buildNumber ?: 'dev',
            commitHash : versionInfo.sha,
            buildDate  : currentDate
    ]
}

repositories {
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
    }
    dependencies {
        dependency("org.apache.commons:commons-lang3:3.5")
        dependency("com.google.guava:guava:19.0")

        dependency("io.projectreactor:reactor-core:3.0.7.RELEASE")
        dependency("io.projectreactor.addons:reactor-test:3.0.6.RELEASE")

        // overrides
        dependency("org.assertj:assertj-core:3.6.1")
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter")
    compile("org.springframework.boot:spring-boot-configuration-processor", optional)
    compile files("lib/jfxrt.jar")
    compile("com.google.guava:guava")
    compile("com.github.nginate:commons-lang:1.0.0")
    compile("com.fasterxml.jackson.core:jackson-databind")
    compile("org.hibernate:hibernate-core")

    compile("org.projectlombok:lombok:1.16.16", optional)
    compile("com.google.code.findbugs:jsr305:3.0.1", optional)

    compile("com.datastax.cassandra:cassandra-driver-core:3.1.3")
    compile("org.springframework.data:spring-data-cassandra")

    runtime("org.codehaus.janino:janino")
}

scmVersion {
    versionIncrementer 'incrementMinorIfNotOnRelease', [releaseBranchPattern: 'release-.*']
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
    distributionType = ALL
}
